AC_INIT(src/gtkhtml.c)

AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE(gtkhtml, 0.0)
 
AM_MAINTAINER_MODE

AM_ACLOCAL_INCLUDE(macros)

GNOME_INIT

AC_PROG_CC
AC_ISC_POSIX
AC_HEADER_STDC
AC_ARG_PROGRAM
AM_PROG_LIBTOOL

GNOME_COMPILE_WARNINGS

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

dnl ******************************
dnl Check for Bonobo
dnl ******************************
try_bonobo=true
bonobo=
bonobo_msg=no
have_bonobo=false
AC_ARG_WITH(bonobo,
	[--{with,without}-bonobo    Compile with Bonobo support or without it],
	if test x$withval = xyes; then
		try_bonobo=true
	fi
)

if $try_bonobo; then
	AC_MSG_CHECKING(for Bonobo > 0.5)
	if gnome-config --libs bonobo > /dev/null 2>&1; then
		vers=`gnome-config --modversion bonobo`
		case $vers
		in
		    bonobo-0.[[012345]]) bonobo_ok=false ;;
		    *) bonobo_ok=true ;;
		esac
	else
		bonobo_ok=false
	fi
	
	if $bonobo_ok; then
		AC_MSG_RESULT(found)
		AC_DEFINE(ENABLE_BONOBO)
		have_bonobo=true
		bonobo=bonobo
		bonobo_msg=yes
	else
		AC_MSG_RESULT(not found)
	fi
fi
AM_CONDITIONAL(BONOBO, $have_bonobo)

dnl
dnl Check for libwww
dnl
AC_PATH_PROG(LIBWWW_CONFIG,libwww-config,no)
if test "$LIBWWW_CONFIG" = no; then
	have_libwww=false
	AC_MSG_ERROR([libwww is required])
else
	LIBWWW_CFLAGS="`$LIBWWW_CONFIG --cflags`"
	LIBWWW_LIBS="`$LIBWWW_CONFIG --libs`"

dnl if we have the mini libwww's, trim the list we link with

dnl	if echo "$LIBWWW_LIBS" | grep wwwcore > /dev/null; then
dnl	    changequote(,)dnl
dnl	    LIBWWW_LIBS="`echo $LIBWWW_LIBS | sed 's/-lxml[^ ]* //g;s/-lwww[^ ]* //g;s/-ldl//;s/-lz//'`"
dnl	    changequote([,])dnl
dnl	    LIBWWW_LIBS="$LIBWWW_LIBS -lwwwzip -lwwwhttp -lwwwmime -lwwwftp -lwwwfile -lwwwdir -lwwwhtml -lwwwcache -lwwwstream -lwwwtrans -lwwwcore -lwwwutils -lwwwapp -lwwwnews -lwwwtelnet -lz"
dnl	fi
	have_libwww=true
	AC_DEFINE(HAVE_LIBWWW)
fi

dnl gtkhtmlConf.sh stuff

GTKHTML_LIBDIR="-L${libdir}"
GTKHTML_INCLUDEDIR="-I${includedir}"
GTKHTML_INCLUDEDIR="$GTKHTML_INCLUDEDIR `$GNOME_CONFIG --cflags gdk_pixbuf`"
GTKHTML_LIBS="-lgtkhtml `$GNOME_CONFIG --libs gdk_pixbuf`"

AC_SUBST(GTKHTML_LIBDIR)
AC_SUBST(GTKHTML_INCLUDEDIR)
AC_SUBST(GTKHTML_LIBS)

AC_SUBST(LIBWWW_CFLAGS)
AC_SUBST(LIBWWW_LIBS)
AM_CONDITIONAL(LIBWWW, $have_libwww)

EXTRA_GNOME_LIBS=`gnome-config --libs gnomeui gdk_pixbuf $bonobo`
EXTRA_GNOME_CFLAGS=`gnome-config --cflags gnomeui gdk_pixbuf $bonobo`
AC_SUBST(EXTRA_GNOME_LIBS)
AC_SUBST(EXTRA_GNOME_CFLAGS)

AC_OUTPUT([
Makefile
macros/Makefile
src/Makefile
glibwww/Makefile
components/Makefile
components/html-editor/Makefile
])
