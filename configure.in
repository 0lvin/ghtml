dnl Initialization

AC_INIT(src/gtkhtml.c)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(gtkhtml, 0.4)

AM_MAINTAINER_MODE
AM_ACLOCAL_INCLUDE(macros)

GNOME_INIT(capplet)

AC_PROG_CC
AC_ISC_POSIX
AC_HEADER_STDC
AC_ARG_PROGRAM
AM_PROG_LIBTOOL

GNOME_COMPILE_WARNINGS

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

AM_PATH_GCONF
AM_GNOME_GETTEXT

dnl GNU regex checking

AC_CHECK_FUNC(re_compile_pattern, AC_DEFINE(HAVE_GNU_REGEX))

dnl Libwww checks.

AC_MSG_CHECKING([for libwww])
if ! `libwww-config --version > /dev/null 2>&1` ; then
	AC_MSG_RESULT([not found])
	have_libwww=false
else
	vers=`libwww-config --version`
	AC_MSG_RESULT([found ("$vers")])

	LIBWWW_CFLAGS="`libwww-config --cflags`"
	LIBWWW_LIBS="`libwww-config --libs`"

	AC_MSG_CHECKING([if libwww has zlib support built-in])
	if ( libwww-config --libs | grep -w -- '-lz' > /dev/null ) ; then
		AC_MSG_RESULT([yes])
		have_libwww=true
	else
		AC_MSG_RESULT([no])
		AC_MSG_WARN([libwww should be built with zlib support to be usable by the test program.])
		AC_MSG_WARN([Your libwww does not seem to have zlib support.])
		have_libwww=false
	fi
fi

if $have_libwww ; then
	AC_DEFINE(HAVE_LIBWWW)
else
	AC_MSG_WARN([\`testgtkhtml' will not be built.])
fi

AC_SUBST(LIBWWW_CFLAGS)
AC_SUBST(LIBWWW_LIBS)
AM_CONDITIONAL(LIBWWW, $have_libwww)

dnl Bonobo checks.

try_bonobo=true
bonobo=
bonobo_msg=no
have_bonobo=false
AC_ARG_WITH(bonobo,
	[--{with,without}-bonobo    Compile with Bonobo support or without it],
	if test x$withval = xno; then
		try_bonobo=false
	fi
	if test x$withval = xyes; then
		try_bonobo=true
	fi
)

if $try_bonobo; then
	AC_MSG_CHECKING(for Bonobo >= 0.15)
	if gnome-config --libs bonobo > /dev/null 2>&1; then
		vers=`gnome-config --modversion bonobo | sed 's/^bonobo-//'`
		case $vers
		in
			0.[[0123456789]])
				bonobo_ok=false ;;

			0.1[[01234]])
				bonobo_ok=false ;;

			*)
				bonobo_ok=true ;;
		esac
	else
		bonobo_ok=false
	fi
	
	if $bonobo_ok; then
		AC_MSG_RESULT([found ("$vers")])
		AC_DEFINE(ENABLE_BONOBO)
		have_bonobo=true
		bonobo=bonobo
		bonobo_msg=yes
	else
		AC_MSG_RESULT([not found])
	fi
fi

AM_CONDITIONAL(BONOBO, $have_bonobo)

dnl Check if Bonobo is OAFized
AC_MSG_CHECKING(if Bonobo uses OAF)
if ( gnome-config --libs bonobo | grep oaf ) > /dev/null 2>&1 ; then
	using_oaf="yes"
	AC_DEFINE(USING_OAF)
else
	using_oaf="no"
fi

AC_MSG_RESULT("$using_oaf")

AM_CONDITIONAL(USING_OAF, test "x$using_oaf" = "xyes")

dnl gnome-print checks.

AC_MSG_CHECKING([for gnome-print >= 0.16])
if gnome-config --libs print > /dev/null 2>&1; then
	vers=`gnome-config --modversion print | sed 's/^gnome-print-//'`
	if [[ "x$vers" = "x" ]] ; then
		gnomeprint_ok=false
	else
		case "$vers" in
			0.[[0123456789]])
				gnomeprint_ok=false ;;

			0.1[[012345]])
				gnomeprint_ok=false ;;

			*)
				gnomeprint_ok=true ;;
		esac
	fi

	AC_MSG_RESULT([found ("$vers")])
else
	AC_MSG_RESULT([not found])
	gnomeprint_ok=false
fi

if test "x$gnomeprint_ok" = "xfalse"; then
	AC_MSG_ERROR([gnome-print 0.16 or later is required to compile GtkHTML.])
fi

dnl gdk-pixbuf checks.

AC_MSG_CHECKING([for gdk-pixbuf >= 0.7.0])

if gnome-config --libs gdk_pixbuf > /dev/null 2>&1; then
	vers=`gnome-config --modversion gdk_pixbuf | sed 's/^gdk-pixbuf-//'`
        major=`echo "$vers" | sed 's/\([[0-9]]*\)\.[[0-9]]*\.[[0-9]]*/\1/'`
        minor=`echo "$vers" | sed 's/[[0-9]]*\.\([[0-9]]*\)\.[[0-9]]*/\1/'`

	if [[ "x$major" = "x" ]] ; then
		gdkpixbuf_ok=false
        elif [[ "x$major" = "x0" ]] ; then
		case "$minor" in
			[[0123456]])
				gdkpixbuf_ok=false ;;
                        *)
				gdkpixbuf_ok=true ;;
		esac
	else
		gdkpixbuf_ok=true
	fi
	AC_MSG_RESULT([found ("$vers")])
else
	gdkpixbuf_ok=false
	AC_MSG_RESULT([not found])
fi

if test x$gdkpixbuf_ok = xfalse ; then
	AC_MSG_ERROR([gdk-pixbuf 0.7.0 or later is required to compile GtkHTML.])
fi

dnl Setup extra GNOME library flags.

EXTRA_GNOME_LIBS=`gnome-config --libs gnomeui gdk_pixbuf print $bonobo`
EXTRA_GNOME_CFLAGS=`gnome-config --cflags gnomeui gdk_pixbuf print $bonobo`

AC_SUBST(EXTRA_GNOME_LIBS)
AC_SUBST(EXTRA_GNOME_CFLAGS)

dnl gtkhtmlConf.sh stuff.

GTKHTML_LIBDIR="-L${libdir}"
GTKHTML_INCLUDEDIR="-I${includedir}"
GTKHTML_INCLUDEDIR="$GTKHTML_INCLUDEDIR `$GNOME_CONFIG --cflags gdk_pixbuf print`"
GTKHTML_LIBS="-lgtkhtml `$GNOME_CONFIG --libs gdk_pixbuf print`"

AC_SUBST(GTKHTML_LIBDIR)
AC_SUBST(GTKHTML_INCLUDEDIR)
AC_SUBST(GTKHTML_LIBS)

dnl Library version information.
 
dnl Increment the following if the interface has additions, changes,
dnl removals.
GTKHTML_CURRENT=2

dnl Increment any time the source changes; set to 0 if you
dnl increment CURRENT.
GTKHTML_REVISION=0

dnl Increment if any interfaces have been added; set to 0
dnl if any interfaces have been removed. removal has 
dnl precedence over adding, so set to 0 if both happened.
GTKHTML_AGE=0

AC_SUBST(GTKHTML_CURRENT)
AC_SUBST(GTKHTML_REVISION)
AC_SUBST(GTKHTML_AGE)

dnl Done.

AC_OUTPUT([
Makefile
gtkhtml.spec
macros/Makefile
src/Makefile
capplet/Makefile
glibwww/Makefile
components/Makefile
components/html-editor/Makefile
])
